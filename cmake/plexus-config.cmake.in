@PACKAGE_INIT@

macro(import_targets type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/plexus-${type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "plexus ${type} libraries were requested but not found")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/plexus-${type}-targets.cmake")
endmacro()

if(NOT TARGET plexus::libplexus)
    set(_type "")

    if(DEFINED PLEXUS_USE_SHARED_LIB)
        if(PLEXUS_USE_SHARED_LIB)
            set(_type "shared")
        else()
            set(_type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/plexus-shared-targets.cmake")
        set(_type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/plexus-static-targets.cmake")
        set(_type "static")
    else()
        set(_type "shared")
    endif()

    import_targets(${_type})
    check_required_components(plexus)

    include(CMakeFindDependencyMacro)
    find_dependency(Boost COMPONENTS system coroutine)
    find_dependency(wormhole COMPONENTS libwormhole)
    find_dependency(tubus COMPONENTS libtubus)

    get_target_property(plexus_INCLUDE_DIRS plexus::libplexus INTERFACE_INCLUDE_DIRECTORIES)
    set(plexus_LIBRARY plexus::libplexus)

    set(plexus_FOUND ON)
    unset(_type)
endif()
